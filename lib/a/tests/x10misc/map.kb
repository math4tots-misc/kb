

test Get
    assert LEN({:}) == 0
    assert LEN({'a': 33}) == 1

    assert REPR({'a': 33}) == '{"a": 33}'

    map = {'a': 33}

    assert map['a'] == 33

    assert 'a' in map
    assert 'b' not in map
end

test SetKeys
    d = {
        {1, 2}: 'A',
        {2, 3}: 'B',
    }

    assert d == {{3, 2}: 'B', {2, 1}: 'A'}
    assert d[{1, 2}] == 'A'
    assert d[{2, 1}] == 'A'
    assert LEN(d) is 2

    d[{2, 1}] = 'C'
    assert d == {{2, 3}: 'B', {1, 2}: 'C'}
end

test ListKeys
    d = {
        [1, 2]: 'LIST1',
        [2, 1]: 'LIST2',
        {1, 2}: 'SET',
    }

    assert LEN(d) == 3
    assert STR(d) == '{[1, 2]: "LIST1", [2, 1]: "LIST2", {1, 2}: "SET"}'
    assert d[[1, 2]] == 'LIST1'
    assert d[[2, 1]] == 'LIST2'
end

test Add
    d = {:}
    assert ADD(d, ['key', 'val']) is d
    assert ADD(d, ['a', 10]) is d

    assert d == {'a': 10, 'key': 'val'}
end

test MapKeys
    # Maps are nto considered hashable, so
    # trying to use them as keys throw an error
    assert throw { {1: 2}: 2 }
end

test ForIn
    list = []
    map = {'a': 55, {'x'}: 7, 2: 5.5}
    for x in map
        ADD(list, x)
    end
    assert list == [[2, 5.5], ["a", 55], [{"x"}, 7]]
end

test Conversion
    list = [['a', 123], [{}, 'xx'], [[1, 2], 5.5]]
    map = MAP(list)
    assert map == {"a": 123, [1, 2]: 5.5, {}: "xx"}
end

test Extend
    map = {1: 2, 3: 4}
    EXTEND(map, {'a': 'b', 'c': 'd', 0: -1})
    assert map == {0: -1, 1: 2, 3: 4, "a": "b", "c": "d"}

    map = {1: 'a', 2: 'b'}
    EXTEND(map, {1: 'xx'})
    assert map == {1: "xx", 2: "b"}
end

test Remove
    map = {1: 2}
    assert LEN(map) == 1

    REMOVE(map, 1)
    assert map == {:}
    assert LEN(map) == 0

    # without the ':', it's an empty set
    assert map != {}
end
